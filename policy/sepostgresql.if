########################################
## <summary>
##     marks as a server process of SE-PostgreSQL.
## </summary>
## <param name="type">
##     <summary>
##     Type marked as a database object type.
##     </summary>
## </param>
#
interface(`sepgsql_server_domain',`
	gen_require(`
		attribute sepgsql_server_type;
	')
	typeattribute $1 sepgsql_server_type;
')

########################################
## <summary>
##     Marks as a SE-PostgreSQL loadable shared library module
## </summary>
## <param name="type">
##     <summary>
##     Type marked as a database object type.
##     </summary>
## </param>
#
interface(`sepgsql_module_object',`
	gen_require(`
		attribute sepgsql_module_type;
	')
	typeattribute $1 sepgsql_module_type;
')

########################################
## <summary>
##     provide a basic permission set for a domain
## </summary>
## <param name="type">
##     <summary>
##     A domain marked as a normal database client type, with no privilege.
##     </summary>
## </param>
#
interface(`__sepgsql_client_domain',`
	gen_require(`
		class database all_database_perms;
		class table all_table_perms;
		class column all_column_perms;
		class tuple all_tuple_perms;
		class procedure all_procedure_perms;
		class blob all_blob_perms;

		type sepgsql_db_t;

		type sepgsql_table_t;
		type sepgsql_sysobj_t;
		type sepgsql_secret_table_t;
		type sepgsql_ro_table_t;
		type sepgsql_fixed_table_t;

		type sepgsql_proc_t;
		type sepgsql_trusted_proc_t;
		type sepgsql_user_proc_t;

		type sepgsql_blob_t;
		type sepgsql_ro_blob_t;
		type sepgsql_secret_blob_t;

		type sepgsql_trusted_domain_t;
	')
	allow $1 sepgsql_db_t : database { getattr access };

	allow $1 sepgsql_table_t : table { getattr use select update insert delete };
	allow $1 sepgsql_table_t : column { getattr use select update insert };
	allow $1 sepgsql_table_t : tuple { use select update insert delete };

	allow $1 sepgsql_sysobj_t : table { getattr use select };
	allow $1 sepgsql_sysobj_t : column { getattr use select };
	allow $1 sepgsql_sysobj_t : tuple { use select };

	allow $1 sepgsql_secret_table_t : table { getattr };
	allow $1 sepgsql_secret_table_t : column { getattr };

	allow $1 sepgsql_ro_table_t : table { getattr use select };
	allow $1 sepgsql_ro_table_t : column { getattr use select };
	allow $1 sepgsql_ro_table_t : tuple { use select };

	allow $1 sepgsql_fixed_table_t : table { getattr use select insert };
	allow $1 sepgsql_fixed_table_t : column { getattr use select insert };
	allow $1 sepgsql_fixed_table_t : tuple { use select insert };

	allow $1 sepgsql_proc_t : procedure { getattr execute };
	allow $1 sepgsql_user_proc_t : procedure { create drop getattr setattr execute };
	allow $1 sepgsql_trusted_proc_t : procedure { getattr execute entrypoint };

	allow $1 sepgsql_blob_t : blob { create drop getattr setattr read write };
	allow $1 sepgsql_ro_blob_t : blob { getattr read };
	allow $1 sepgsql_secret_blob_t : blob { getattr };

	type_transition $1 sepgsql_database_type : procedure sepgsql_user_proc_t;

	# trusted procedure
	type_transition $1 sepgsql_trusted_proc_t : process sepgsql_trusted_domain_t;
	allow $1 sepgsql_trusted_domain_t : process { transition };
')

########################################
## <summary>
##     marks as a client process of SE-PostgreSQL.
## </summary>
## <param name="type">
##     <summary>
##     A domain marked as a normal database client type, with no privilege.
##     </summary>
## </param>
#
interface(`sepgsql_client_domain',`
	gen_require(`
		class database all_database_perms;
		class table all_table_perms;
		class column all_column_perms;
		class tuple all_tuple_perms;
		class procedure all_procedure_perms;
		class blob all_blob_perms;

		type sepgsql_table_t;
		type sepgsql_sysobj_t;
	')

	__sepgsql_client_domain($1)
	tunable_policy(`sepgsql_enable_users_ddl',`
		allow $1 sepgsql_table_t : table { create drop setattr };
		allow $1 sepgsql_table_t : column { create drop setattr };
		allow $1 sepgsql_sysobj_t : tuple { update insert delete };
	')
')

########################################
## <summary>
##     marks as a unconfined client process of SE-PostgreSQL.
## </summary>
## <param name="type">
##     <summary>
##     A domain marked as an administrative type.
##     </summary>
## </param>
#
interface(`sepgsql_unconfined_domain',`
	gen_require(`
		attribute sepgsql_database_type;
		attribute sepgsql_table_type;
		attribute sepgsql_procedure_type;
		attribute sepgsql_blob_type;
		attribute sepgsql_module_type;
		attribute sepgsql_server_type;

		type sepgsql_table_t, sepgsql_sysobj_t;
		type sepgsql_user_proc_t;

		bool sepgsql_enable_unconfined;
	')

	tunable_policy(`sepgsql_enable_unconfined',`
		allow $1 sepgsql_database_type : database all_database_perms;
		allow $1 sepgsql_table_type : table all_table_perms;
		allow $1 sepgsql_table_type : column all_column_perms;
		allow $1 sepgsql_table_type : tuple all_tuple_perms;
		allow $1 { sepgsql_procedure_type - sepgsql_user_proc_t } : procedure all_procedure_perms;
		allow $1 sepgsql_user_proc_t : procedure { create drop getattr setattr relabelfrom relabelto };
		allow $1 sepgsql_blob_type : blob all_blob_perms;
		allow $1 sepgsql_module_type : database { install_module };
		allow $1 sepgsql_server_type : blob { import export };

		type_transition $1 sepgsql_database_type : procedure sepgsql_proc_t;
	',`
		__sepgsql_client_domain($1)
	')
	tunable_policy(`!sepgsql_enable_unconfined && sepgsql_enable_users_ddl',`
		allow $1 sepgsql_table_t : table { create drop setattr };
		allow $1 sepgsql_table_t : column { create drop setattr };
		allow $1 sepgsql_sysobj_t : tuple { update insert delete };
	')
')
